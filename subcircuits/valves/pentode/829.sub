*-----------------------------------------------------------------------
* Filename:		829s.inc  03/04/98  V1
* Simulator:		Spice 3f4
* Device type:		Power beam tetrode
* Device model:		829-B
*
* Author:		Duncan Munro
* Date:			3/4/98
* Copyright:		(C)1998-2000 Duncan Amplification
* 
* The following parameters are not modelled:
*
*   (1) Heater
*   (2) Grid current is an approximation
*
* Please note that this model is provided "as is" and
* no warranty is provided in respect of its suitability
* for any application.
*
* This model is provided for educational and non-profit use.
*
* Email queries to postmaster@duncanamps.com
*
* Pins   A  Anode
*        S  Screen
*        G  Grid
*        K  Cathode
*
*-----------------------------------------------------------------------

.SUBCKT 829HALF A S G K
*
* Calculate contribution to cathode current
*
Bat	at  0	V=0.636*ATAN(V(A,K)/22)
Bgs	gs  0	V=URAMP(V(S,K)/19+V(G,K)/2+V(A,K)/1400)
Bgs2	gs2 0	V=V(gs)^1.5
Bcath	cc  0	V=V(gs2)*V(at)*2
*
* Calculate anode current
*
Ba	A   K	I=3.2E-3*V(cc)
*
* Calculate screen current
*
Bscrn	sc  0	V=V(gs2)*(1.2-V(at))
Bs	S   K	I=1.8E-3*V(sc)
*
* Grid current (approximation - does not model low va/vs)
*
Bg	G   K	I=(URAMP(V(G,K)+1)^1.5)*500E-6
*
* Capacitances
*
Cg1	G	K	14.5p
Cak	A	K	7.0p
Cg1a	G	A	0.12p

.ENDS 

*======================================================================
*
* This is the 829 subcircuit combining the 2 halves
* of the model with a common screen grid
*
* Pins are: 	A1  Anode (plate) 1
*		A2  Anode 2
*		S   Screen
*		G1  Grid 1
*		G2  Grid 2
*		K   Cathode
*
*======================================================================

.SUBCKT 829 A1 A2 S G1 G2 K
XV1 	A1 S G1 K 829HALF
XV2	A2 S G2 K 829HALF
.ENDS

